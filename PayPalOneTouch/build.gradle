import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'com.android.library'
    id 'de.marcphilipp.nexus-publish'
    id 'signing'
}

ext {
    configFileText = new File("${projectDir}/otc-config.android.json").getText('UTF-8')
    config = JsonOutput.toJson(new JsonSlurper().parseText(configFileText)).replaceAll("\"", "\\\\\"")
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        buildConfigField "String", "CONFIGURATION", "\"${config}\""
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.1.0'

    api project(':Core')
    api project(':PayPalDataCollector')

    testImplementation 'org.robolectric:robolectric:4.1'
    testImplementation 'org.skyscreamer:jsonassert:1.4.0'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.6'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.6'
    testImplementation project(':TestUtils')
}

// region signing and publishing

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

signing {
    required {
        !version.endsWith("SNAPSHOT") && !version.endsWith("DEVELOPMENT")
    }
    sign publishing.publications
}

nexusPublishing {
    // give nexus sonatype more time to initialize the staging repository
    clientTimeout = Duration.ofMinutes(3)
    repositories {
        sonatype()
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocsJar

                groupId = 'com.paypal.android.sdk'
                artifactId = 'paypal-one-touch'
                version = rootProject.versionName

                pom {
                    name = 'paypal-one-touch'
                    packaging = 'aar'
                    description = 'PayPal SDK for handling app and browser switch based payments.'
                    url = 'https://github.com/braintree/braintree_android'

                    scm {
                        url = 'scm:git@github.com:braintree/braintree_android.git'
                        connection = 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection = 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

// endregion
