plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

android {
    namespace "com.braintreepayments.api.localpayment"
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile file(System.env['KEYSTORE_FILE'] ?: 'debug.keystore')
            storePassword System.env['KEYSTORE_PASSWORD'] ?: 'android'
            keyAlias System.env['KEY_ALIAS'] ?: 'androiddebugkey'
            keyPassword System.env['KEY_PASSWORD'] ?: 'android'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    compileOptions {
        sourceCompatibility versions.javaSourceCompatibility
        targetCompatibility versions.javaTargetCompatibility
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("11"))
        }
    }
}

dependencies {
    implementation deps.appCompat

    api project(':BraintreeCore')
    implementation project(':DataCollector')

    testImplementation deps.robolectric
    testImplementation deps.mockitoCore
    testImplementation deps.jsonAssert
    testImplementation deps.mockk
    testImplementation project(':TestUtils')

    androidTestImplementation project(':TestUtils')
    androidTestImplementation deps.androidxTestRules
    androidTestImplementation deps.androidxTestRunner
    androidTestImplementation deps.junitTest
}

// region signing and publishing

project.ext.name = "local-payment"
project.ext.pom_name = "local-payment"
project.ext.group_id = "com.braintreepayments.api"
project.ext.version = rootProject.version
project.ext.pom_desc = "Local Payment dependencies for Braintree Android."

apply from: rootProject.file("gradle/gradle-publish.gradle")

// endregion
