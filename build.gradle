buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0'

        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

ext.braintreeDependency = [
        googlePayment: 'com.braintreepayments.api:google-payment:3.1.1',
        playServicesWallet: 'com.google.android.gms:play-services-wallet:16.0.1'
]

allprojects {
    repositories {
        flatDir {
            dirs "${rootDir}/libs"
        }
        google()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/releases/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url 'file:///tmp/maven/'
        }
    }

    apply plugin: 'maven'

    def sonatypeUsername = System.env['SONATYPE_USERNAME']
    def sonatypePassword = System.env['SONATYPE_PASSWORD']

    version = '3.8.0'
    ext {
        compileSdkVersion = 28
        minSdkVersion = 21
        targetSdkVersion = 28
        versionCode = 111
        versionName = version
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                if (version.endsWith("DEVELOPMENT")) {
                    repository(url: 'file:///tmp/maven/')
                } else {
                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    url 'https://github.com/braintree/braintree_android'

                    scm {
                        url 'scm:git@github.com:braintree/braintree_android.git'
                        connection 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id 'devs'
                            name 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }


    apply plugin: 'signing'
    signing {
        required {
          !version.endsWith("SNAPSHOT") && !version.endsWith("DEVELOPMENT")
        }
        sign configurations.archives
    }
}
