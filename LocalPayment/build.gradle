plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.dokka
    alias libs.plugins.kotlin.parcelize
}

android {
    namespace "com.braintreepayments.api.localpayment"
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    compileOptions {
        sourceCompatibility versions.javaSourceCompatibility
        targetCompatibility versions.javaTargetCompatibility
    }

    kotlinOptions {
        jvmTarget = versions.javaTargetCompatibility.toString()
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(versions.javaTargetCompatibility.toString()))
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat

    api project(':BraintreeCore')
    implementation project(':DataCollector')

    testImplementation libs.robolectric
    testImplementation libs.mockito.core
    testImplementation libs.json.assert
    testImplementation libs.mockk
    testImplementation libs.test.parameter.injector
    testImplementation project(':TestUtils')

    androidTestImplementation project(':TestUtils')
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.junit
}

// region signing and publishing

project.ext.name = "local-payment"
project.ext.pom_name = "local-payment"
project.ext.group_id = "com.braintreepayments.api"
project.ext.version = rootProject.version
project.ext.pom_desc = "Local Payment dependencies for Braintree Android."

apply from: rootProject.file("gradle/gradle-publish.gradle")

// endregion
