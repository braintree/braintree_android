plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    namespace "com.braintreepayments.api.sharedutils"
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        buildConfigField "String", "VERSION_NAME", "\"${rootProject.versionName}\""
    }

    signingConfigs {
        release {
            storeFile file(System.env['KEYSTORE_FILE'] ?: 'debug.keystore')
            storePassword System.env['KEYSTORE_PASSWORD'] ?: 'android'
            keyAlias System.env['KEY_ALIAS'] ?: 'androiddebugkey'
            keyPassword System.env['KEY_PASSWORD'] ?: 'android'
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    compileOptions {
        sourceCompatibility versions.javaSourceCompatibility
        targetCompatibility versions.javaTargetCompatibility
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("11"))
        }
    }
}

dependencies {
    implementation deps.annotation
    implementation deps.kotlinStdLib

    testImplementation deps.junit
    testImplementation deps.androidxTestCore
    testImplementation deps.mockitoCore
    testImplementation deps.robolectric
    testImplementation deps.mockitoCore

    androidTestImplementation deps.androidxTestRunner
    androidTestImplementation deps.junitTest
    androidTestImplementation deps.appCompat
}

// region signing and publishing

project.ext.name = "shared-utils"
project.ext.pom_name = "shared-utils"
project.ext.group_id = "com.braintreepayments.api"
project.ext.version = rootProject.version
project.ext.pom_desc = "Library containing shared components used across Braintree\'s Android SDKs."

apply from: rootProject.file("gradle/gradle-publish.gradle")

// endregion
