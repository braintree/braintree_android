plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
}

android {
    namespace "com.braintreepayments.api.paypalmessaging"
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        // required to be out of sync with other modules due to messaging SDK min version
        minSdkVersion rootProject.minSdkVersionPayPalMessaging
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "VERSION_NAME", "\"${rootProject.versionName}\""
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    compileOptions {
        sourceCompatibility versions.javaSourceCompatibility
        targetCompatibility versions.javaTargetCompatibility
    }

    kotlinOptions {
        jvmTarget = versions.javaTargetCompatibility.toString()
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(versions.javaTargetCompatibility.toString()))
        }
    }
}

dependencies {
    api project(':BraintreeCore')

    implementation libs.androidx.core.ktx
    implementation libs.kotlin.stdlib
    implementation libs.androidx.appcompat
    implementation(libs.paypal.messages)

    testImplementation libs.robolectric
    testImplementation libs.json.assert
    testImplementation libs.androidx.junit
    testImplementation libs.mockk
    testImplementation libs.mockito.core
    testImplementation libs.test.parameter.injector
    testImplementation project(':TestUtils')
}

// region signing and publishing

project.ext.name = "paypal-messaging"
project.ext.pom_name = "paypal-messaging"
project.ext.group_id = "com.braintreepayments.api"
project.ext.version = rootProject.version
project.ext.pom_desc = "PayPal Messaging dependencies for Braintree Android."

apply from: rootProject.file("gradle/gradle-publish.gradle")

// endregion