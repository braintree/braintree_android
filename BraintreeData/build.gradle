apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 6
        versionName '1.0.5'
    }
}

sdkManager {
    emulatorVersion 'android-19'
}

dependencies {
    compile files('libs/devicecollector-sdk-2.5.jar')
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task releaseJar(type: Jar, dependsOn: android.libraryVariants.release.javaCompile) {
    from projectDir.path + '/build/intermediates/classes/release/'
    exclude { details -> details.file.name.endsWith('.xml') }

    from {
        configurations.compile.findAll {
            it.getName() != 'android.jar'
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

artifacts {
    archives javadocsJar
    archives sourcesJar
    archives releaseJar
}
signing {
    sign configurations.archives
}

def sonatypeUsername = System.properties.containsKey('sonatypeUsername') ? System.properties['sonatypeUsername'] : ''
def sonatypePassword = System.properties.containsKey('sonatypePassword') ? System.properties['sonatypePassword'] : ''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'data'

            pom.project {
                name 'data'
                groupId = 'com.braintreepayments.api'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'Fraud tools for Braintree integrations on Android.'
                url 'https://github.com/braintree/braintree_android'

                scm {
                    url 'scm:git@github.com:braintree/braintree_android.git'
                    connection 'scm:git@github.com:braintree/braintree_android.git'
                    developerConnection 'scm:git@github.com:braintree/braintree_android.git'
                }

                developers {
                    developer {
                        id 'devs'
                        name 'Braintree Payments'
                    }
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

