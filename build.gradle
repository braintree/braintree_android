buildscript {
    repositories {
        mavenCentral()
        google()
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }

    def sdkTargetJavaVersion = JavaVersion.VERSION_1_8

    // We are restricted to room version 2.2.6 since we still offer Java 7 support. Upgrading to
    // newer versions of room that require Java 8 will introduce a breaking change to our SDK
    def java7SafeRoomVersion = "2.2.6"

    // On M1 Macs, switch room version to 2.4.0 to fix java.lang.ExceptionInInitializerError
    def m1MacSafeRoomVersion = "2.4.0"

    // The 'CI' build variable will evaluate to 'true' when build script is executed via GitHub Actions
    // Ref: https://docs.github.com/en/actions/learn-github-actions/environment-variables#default-environment-variables
    def roomVersion = System.getenv('CI') ? java7SafeRoomVersion : m1MacSafeRoomVersion

    ext.versions = [
            "kotlin"         : "1.7.10",
            "androidxTest"   : "1.4.0",
            // NEXT MAJOR VERSION: Use a single up-to-date room version once we remove Java 7 support in favor of Java 11
            "room"           : roomVersion,
            "playServices"   : "19.2.1",
            "javaSourceCompatibility": sdkTargetJavaVersion,
            "javaTargetCompatibility": sdkTargetJavaVersion,
    ]

    ext.deps = [
            "appCompat"                  : "androidx.appcompat:appcompat:1.3.1",
            "annotation"                 : "androidx.annotation:annotation:1.2.0",
            "coreKtx"                    : "androidx.core:core-ktx:1.1.0",

            // NEXT_MAJOR_VERSION: upgrade to 2.4.0 (or latest) when Java 7 support is explicitly dropped
            "lifecycleRuntime"           : "androidx.lifecycle:lifecycle-runtime:2.3.0",

            "kotlinStdLib"               : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
            "kotlinTest"                 : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",
            // This library doesn't seem to follow the versioning pattern of the other jetbrains
            // kotlin libraries. Make sure to keep this dependency in line with the kotlin version used.
            "kotlinCoroutinesCore"       : "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2",

            "browserSwitch"              : "com.braintreepayments.api:browser-switch:2.6.1",
            "cardinal"                   : "org.jfrog.cardinalcommerce.gradle:cardinalmobilesdk:2.2.7-5",
            "samsungPay"                 : "com.samsung.android.spay:sdk:2.5.01",
            "playServicesWallet"         : "com.google.android.gms:play-services-wallet:${versions.playServices}",

            // NEXT_MAJOR_VERSION: upgrade to 2.7.1 (or latest) when Java 7 support is explicitly dropped
            "work"                       : "androidx.work:work-runtime:2.7.0-alpha05",
            "workTesting"                : "androidx.work:work-testing:2.5.0",

            "roomCompiler"               : "androidx.room:room-compiler:${versions.room}",
            "roomRuntime"                : "androidx.room:room-runtime:${versions.room}",

            "androidxTestRules"          : "androidx.test:rules:${versions.androidxTest}",
            "androidxTestRunner"         : "androidx.test:runner:${versions.androidxTest}",
            "androidxTestCore"           : "androidx.test:core:${versions.androidxTest}",

            "junit"                      : "junit:junit:4.13",
            "junitTest"                  : "androidx.test.ext:junit:1.1.3",
            "robolectric"                : "org.robolectric:robolectric:4.7.3",
            "dexmakerMockito"            : "com.google.dexmaker:dexmaker-mockito:1.2",
            "mockitoCore"                : "org.mockito:mockito-core:5.3.1",
            "jsonAssert"                 : "org.skyscreamer:jsonassert:1.5.1",
            "mockk"                      : "io.mockk:mockk:1.13.4",
    ]

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2'
        classpath 'de.marcphilipp.gradle:nexus-publish-plugin:0.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.4.1'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.7.20'
        classpath 'org.jetbrains.dokka:android-documentation-plugin:1.7.20'
    }
}

plugins {
    id 'org.jetbrains.dokka' version '1.7.10'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'io.gitlab.arturbosch.detekt' version '1.22.0'
}

ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID') ?: ''
ext["signing.password"] = System.getenv('SIGNING_KEY_PASSWORD') ?: ''
ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_KEY_FILE') ?: ''

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

version '4.41.1-SNAPSHOT'
group 'com.braintreepayments'
ext {
    compileSdkVersion = 34
    minSdkVersion = 21
    minSdkVersionPayPalMessaging = 23
    versionCode = 186
    targetSdkVersion = 34
    versionName = version
}

nexusPublishing {
    packageGroup = project.hasProperty('nexusPackageGroup') ? project.getProperty('nexusPackageGroup') : 'com.braintreepayments'

    repositories {
        sonatype {
            username = System.getenv('SONATYPE_NEXUS_USERNAME') ?: ''
            password = System.getenv('SONATYPE_NEXUS_PASSWORD') ?: ''
        }
    }

    transitionCheckOptions {
        // give nexus sonatype more time to close the staging repository
        delayBetween.set(Duration.ofSeconds(20))
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.22.0"
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-rules-libraries:1.22.0"
}

detekt {
    toolVersion = "1.22.0"
    config = files("detekt/detekt-config.yml")
    input = files(
            "BraintreeCore/src",
            "AmericanExpress/src",
            "BraintreeDataCollector/src",
            "Card/src",
            "Demo/src",
            "GooglePay/src",
            "LocalPayment/src",
            "PayPal/src",
            "PayPalDataCollector/src",
            "PayPalMessaging/src",
            "PayPalNativeCheckout/src",
            "SEPADirectDebit/src",
            "SharedUtils/src",
            "TestUtils/src",
            "ThreeDSecure/src",
            "Venmo/src",
    )
    autoCorrect = project.hasProperty('detektAutoCorrect')
    reports {
        html {
            enabled = true
            destination = file("build/reports/detekt_report.html")
        }
    }
}

subprojects {
    repositories {
        maven {
            url "https://cardinalcommerceprod.jfrog.io/artifactory/android"
            credentials {
                username 'braintree_team_sdk'
                password 'AKCp8jQcoDy2hxSWhDAUQKXLDPDx6NYRkqrgFLRc3qDrayg6rrCbJpsKKyMwaykVL8FWusJpp'
            }
        }
        flatDir {
            dirs "${rootDir}/libs"
        }
    }
}

dokkaHtmlMultiModule.configure {
    // redirect dokka output to GitHub pages root directory
    outputDirectory.set(project.file("docs"))
}

task changeGradleReleaseVersion {
    doLast {
        def gradleFile = new File('build.gradle')
        def gradleFileText = gradleFile.text.replaceFirst("\\nversion '\\d+\\.\\d+\\.\\d+(-.*)?'", "\nversion '" + versionParam + "'")
        gradleFile.write(gradleFileText)
    }
}

task changeREADMEVersion {
    doLast {

        def readmeFile = new File('README.md')
        def readmeFileText = readmeFile.text.replaceFirst(":card:\\d+\\.\\d+\\.\\d+'", ":card:" + versionParam + "'")
        readmeFile.write(readmeFileText)
    }
}

task changeMigrationGuideVersion {
    doLast {
        def migrationGuideFile = new File('v4_MIGRATION_GUIDE.md')
        def migrationGuideFileText = migrationGuideFile.text.replaceAll(":\\d+\\.\\d+\\.\\d+(-.*)?'", ":" + versionParam + "'")
        migrationGuideFile.write(migrationGuideFileText)

        def newMigrationGuideFile = new File('v4.9.0+_MIGRATION_GUIDE.md')
        def newMigrationGuideFileText = newMigrationGuideFile.text.replaceAll(":\\d+\\.\\d+\\.\\d+(-.*)?'", ":" + versionParam + "'")
        newMigrationGuideFile.write(newMigrationGuideFileText)
    }
}

task updateCHANGELOGVersion {
    doLast {
        def changelogFile = new File('CHANGELOG.md')
        def changelogFileText = changelogFile.text.replaceFirst("## unreleased", "## " + versionParam + " (" + new Date().format('yyyy-MM-dd') + ")")
        changelogFile.write(changelogFileText)
    }
}

task incrementSNAPSHOTVersion {
    doLast {
        def gradleFile = new File('build.gradle')
        def (major, minor, patch) = versionParam.tokenize('.')
        def patchInteger = patch.toInteger()
        def newVersion = "$major.$minor.${patchInteger + 1}-SNAPSHOT"
        def gradleFileText = gradleFile.text.replaceFirst("\\nversion '\\d+\\.\\d+\\.\\d+(-.*)?'", "\nversion '" + newVersion + "'")
        gradleFile.write(gradleFileText)

        // update README snapshot version
        def readmeFile = new File('README.md')
        def readmeFileText = readmeFile.text.replaceFirst(":card:\\d+\\.\\d+\\.\\d+-SNAPSHOT'", ":card:" + newVersion + "'")
        readmeFile.write(readmeFileText)
    }
}

task incrementVersionCode {
    doLast {
        def gradleFile = new File('build.gradle')
        def versionText = gradleFile.text.find("versionCode = \\d+")
        def params = versionText.split("=")
        def newVersionCode = params[1].trim().toInteger() + 1
        def gradleFileText = gradleFile.text.replaceFirst("versionCode = \\d+", "versionCode = " + newVersionCode)
        gradleFile.write(gradleFileText)
    }
}
