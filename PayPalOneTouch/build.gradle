import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'com.android.library'

ext {
    configFileText = new File("${projectDir}/otc-config.android.json").getText('UTF-8')
    config = JsonOutput.toJson(new JsonSlurper().parseText(configFileText)).replaceAll("\"", "\\\\\"")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "String", "CONFIGURATION", "\"${config}\""
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compile 'com.android.support:support-annotations:25.0.0'

    compile project(':Core')
    compile project(':PayPalDataCollector')

    testCompile 'org.robolectric:robolectric:3.1.4'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.skyscreamer:jsonassert:1.4.0'
    testCompile project(':TestUtils')
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.paypal"
}

group = 'com.paypal.android.sdk'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'paypal-one-touch'

            pom.project {
                name 'paypal-one-touch'
                groupId = 'com.paypal.android.sdk'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'PayPal SDK for handling app and browser switch based payments.'
            }

            pom.whenConfigured { MavenPom pom ->
                pom.dependencies.each {
                    if (it.artifactId == 'support-annotations') {
                        it.optional = true
                    }
                }
            }
        }
    }
}
