apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.braintreepayments.demo"
        minSdkVersion 18
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
    }

    lintOptions {
        disable 'IconMissingDensityFolder'
        warning 'InvalidPackage'
        warning 'GradleCompatible'
        textReport true
        textOutput 'stdout'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    dataBinding {
        enabled = true
    }
}

configurations.all {
    // Force remove dependency from play-services-wallet
    exclude group: 'com.google.android.gms', module: 'play-services-maps'
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'

    implementation('com.squareup.retrofit:retrofit:1.9.0') {
        exclude module: 'com.google.gson'
    }

    implementation project(':Braintree')

    implementation 'com.google.android.gms:play-services-wallet:16.0.1'

    // Visa checkout dependencies
    implementation 'com.braintreepayments.api:visa-checkout:2.0.0'
    implementation(group: 'com.visa.checkout', name: 'visacheckout-android-sdk', version: '6.6.0', ext: 'aar')
    implementation 'com.squareup.okio:okio:1.13.0'
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'de.greenrobot:eventbus:2.4.1'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:multidex:1.0.3'

    implementation 'io.card:android-sdk:5.5.1'
    implementation('com.braintreepayments.api:drop-in:3.7.0') {
        exclude group: 'com.braintreepayments.api', module: 'braintree'
    }

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4'

    androidTestImplementation 'com.lukekorth:device-automator:0.3.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.2'
    androidTestImplementation project(':TestUtils')

    testImplementation 'junit:junit:4.12'
}



task demoApkSize {
  group 'Braintree'
  description 'Prints the current size of the Demo apk'

  def apkDir = fileTree('./build/outputs/apk')
  def apks = apkDir.matching {
    include '*.apk'
    exclude '*unaligned*'
  }
  apks.visit { apk ->
    def apkName = apk.getName()
    def mb = apk.getSize() / 1024 / 1000
    mb = mb.setScale(2, BigDecimal.ROUND_UP)
    println "Size of ${apkName} is ${mb} MB"
  }
}

project.tasks.findAll {
  if (it.name.startsWith('assemble')) {
    it.finalizedBy demoApkSize
  }
}
