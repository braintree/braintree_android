import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'com.android.library'

def getBuildVersion = { ->
    "git describe --match=*[0-9]*.[0-9]*.*[0-9] --tags --dirty --always".execute().text.trim()
}

ext {
    product_version = getBuildVersion()

    configFileText = new File("${projectDir}/otc-config.android.json").getText('UTF-8')
    config = JsonOutput.toJson(new JsonSlurper().parseText(configFileText)).replaceAll("\"", "\\\\\"")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "String", "PRODUCT_NAME", "\"PayPalOneTouch-Android\""
        buildConfigField "String", "PRODUCT_VERSION", "\"${product_version}\""
        buildConfigField "String", "CONFIGURATION", "\"${config}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:support-annotations:[23.0.0,24.0.0)'

    compile project(':Core')
    compile project(':PayPalDataCollector')

    testCompile project(path: ':TestUtils', configuration: 'unitTestDebug')
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.paypal"
}

group = 'com.paypal.android.sdk'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'paypal-one-touch'

            pom.project {
                name 'paypal-one-touch'
                groupId = 'com.paypal.android.sdk'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'PayPal SDK for handling app and browser switch based payments.'
            }

            pom.whenConfigured { MavenPom pom ->
                pom.dependencies.each {
                    if (it.artifactId == 'support-annotations') {
                        it.optional = true
                    }
                }
            }
        }
    }
}
