buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'

        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

allprojects {
    repositories {
        flatDir {
            dirs "${rootDir}/libs"
        }
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/releases/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    apply plugin: 'maven'

    def sonatypeUsername = System.env['SONATYPE_USERNAME']
    def sonatypePassword = System.env['SONATYPE_PASSWORD']

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    url 'https://github.com/braintree/braintree_android'

                    scm {
                        url 'scm:git@github.com:braintree/braintree_android.git'
                        connection 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id 'devs'
                            name 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }


    apply plugin: 'signing'
    signing {
        required { !version.endsWith("SNAPSHOT") }
        sign configurations.archives
    }

    version = '2.7.0'
    ext {
        compileSdkVersion = 26
        buildToolsVersion = '26.0.0'
        minSdkVersion = 16
        targetSdkVersion = 26
        versionCode = 71
        versionName = version
    }
}
