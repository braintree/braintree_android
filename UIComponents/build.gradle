plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.dokka
}

android {
    namespace 'com.braintreepayments.api.uicomponents'
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    compileOptions {
        sourceCompatibility versions.javaSourceCompatibility
        targetCompatibility versions.javaTargetCompatibility
    }

    kotlinOptions {
        jvmTarget = versions.javaTargetCompatibility.toString()
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.3"
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(versions.javaTargetCompatibility.toString()))
        }
    }
}

dependencies {
    api project(':BraintreeCore')
    api project(':PayPal')
    api project(':Venmo')

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.runtime
    implementation libs.android.compose
    implementation libs.androidx.compose.viewModel

    def composeBom = platform(libs.androidx.compose.bom)
    implementation composeBom
    implementation libs.androidx.material3

    testImplementation libs.kotlin.test
    testImplementation project(':TestUtils')
}

// region signing and publishing

project.ext.name = "ui-components"
project.ext.pom_name = "ui-components"
project.ext.group_id = "com.braintreepayments.api"
project.ext.version = rootProject.version
project.ext.pom_desc = "UI Components for Braintree Android."

apply from: rootProject.file("gradle/gradle-publish.gradle")

// endregion