apply plugin: 'maven-publish'
apply plugin: 'signing'

ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID') ?: ''
ext["signing.password"] = System.getenv('SIGNING_KEY_PASSWORD') ?: ''
ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_KEY_FILE') ?: ''

android {
    signingConfigs {
        release {
            storeFile file(System.env['KEYSTORE_FILE'] ?: 'debug.keystore')
            storePassword System.env['KEYSTORE_PASSWORD'] ?: 'android'
            keyAlias System.env['KEY_ALIAS'] ?: 'androiddebugkey'
            keyPassword System.env['KEY_PASSWORD'] ?: 'android'
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId project.ext.group_id
                version project.ext.version
                artifactId project.ext.name
                from components.release

                pom {
                    name = project.ext.pom_name ?: ''
                    packaging = POM_PACKAGING
                    description = project.ext.pom_desc ?: ''
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENSE_NAME
                            url = POM_LICENSE_URL
                            distribution = POM_LICENSE_DISTRIBUTION
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }
                    }
                    scm {
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                        url = POM_SCM_URL
                    }
                }
            }
        }
    }

    def isCiEnvironment = System.getenv('CI')

    signing {
        required { isCiEnvironment }
        if(isCiEnvironment) {
            sign publishing.publications
            sign configurations.archives
        }
    }
}
