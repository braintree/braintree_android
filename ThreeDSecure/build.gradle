 plugins {
    id 'com.android.library'
    id 'de.marcphilipp.nexus-publish'
    id 'signing'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        consumerProguardFiles 'proguard.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }
}

dependencies {
    api project(':BraintreeCore')
    api project(':Card')

    implementation deps.cardinal
    implementation deps.appCompat
    implementation deps.lifecycleRuntime

    androidTestImplementation deps.dexmakerMockito
    androidTestImplementation deps.androidxTestRules
    androidTestImplementation deps.androidxTestRunner
    androidTestImplementation deps.junitTest
    androidTestImplementation project(':TestUtils')

    testImplementation deps.robolectric
    testImplementation deps.powermockJunitLegacy
    testImplementation deps.powermockRuleLegacy
    testImplementation deps.powermockMockitoLegacy
    testImplementation deps.powermockClassloadingLegacy
    testImplementation deps.jsonAssert
    testImplementation project(':TestUtils')
}

// region signing and publishing

 task javadocs(type: Javadoc) {
     source = android.sourceSets.main.java.srcDirs
     classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
     failOnError false
 }

 task javadocsJar(type: Jar, dependsOn: javadocs) {
     archiveClassifier.set('javadoc')
     from javadocs.destinationDir
 }

 task sourcesJar(type: Jar) {
     archiveClassifier.set('sources')
     from android.sourceSets.main.java.srcDirs
 }

 signing {
     required {
         !version.endsWith("SNAPSHOT") && !version.endsWith("DEVELOPMENT")
     }
     sign publishing.publications
 }

 nexusPublishing {
     // give nexus sonatype more time to initialize the staging repository
     clientTimeout = Duration.ofMinutes(3)
     repositories {
         sonatype()
     }
 }

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocsJar

                groupId = 'com.braintreepayments.api'
                artifactId = 'three-d-secure'
                version = rootProject.versionName

                pom {
                    name = 'three-d-secure'
                    packaging = 'aar'
                    description = '3DS dependencies for Braintree Android.'
                    url = 'https://github.com/braintree/braintree_android'

                    scm {
                        url = 'scm:git@github.com:braintree/braintree_android.git'
                        connection = 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection = 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

 // endregion

