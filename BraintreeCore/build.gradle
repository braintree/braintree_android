plugins {
    id 'com.android.library'
    id 'de.marcphilipp.nexus-publish'
    id 'signing'
}

def DEVELOPMENT_URL = System.properties['DEVELOPMENT_URL'] ?: '"http://10.0.2.2:3000/"';

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        consumerProguardFiles 'proguard.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

def playServicesWallet = "com.google.android.gms:play-services-wallet:${rootProject.playServicesWalletVersion}"
def googlePayment = "com.braintreepayments.api:google-payment:${rootProject.googlePaymentVersion}"

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    compileOnly(playServicesWallet) {
        exclude module: 'support-v4'
    }
    compileOnly project(':BraintreeDataCollector')
    compileOnly project(':ThreeDSecure')

    // TODO: remove each api reference when it's corresponding feature client is created
    api googlePayment
    api project(':SharedUtils')
    api project(':Card')
    api project(':PayPal')
    api project(':LocalPayment')
    api project(':AmericanExpress')
    api project(':UnionPay')
    api project(':Venmo')

    androidTestImplementation playServicesWallet
    androidTestImplementation project(':BraintreeDataCollector')
    androidTestImplementation project(':ThreeDSecure')
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation project(':TestUtils')

    testImplementation playServicesWallet
    testImplementation project(':BraintreeDataCollector')
    testImplementation 'org.robolectric:robolectric:4.1'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.6'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.6'
    testImplementation 'org.skyscreamer:jsonassert:1.4.0'
    testImplementation project(':TestUtils')
    testImplementation project(':ThreeDSecure')
}

android.buildTypes.each { type ->
    type.buildConfigField 'String', 'DEVELOPMENT_URL', DEVELOPMENT_URL
}

task runAllTests {
    /*
     * Empty task to run via command line Rakefile or ci.sh to indicate that tests that depend on
     * setting up external services (like the test invalid https server) are set up correctly.
     * Otherwise, these tests will be skipped
     */
}

gradle.taskGraph.whenReady { taskGraph ->
    def RUN_ALL_TESTS = taskGraph.hasTask(':Braintree:runAllTests')
    android.buildTypes.debug.buildConfigField('boolean', 'RUN_ALL_TESTS', '' + RUN_ALL_TESTS)
}

// region signing and publishing

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false

    // Used to exclude docstrings from public site
    exclude "com/braintreepayments/api/PayPalTwoFactorAuth.java"
    exclude "com/braintreepayments/api/PayPalTwoFactorAuthSharedPreferences.java"
    exclude "com/braintreepayments/api/model/PayPalTwoFactorAuthRequest.java"
    exclude "com/braintreepayments/api/model/PayPalTwoFactorAuthResponse.java"
    exclude "com/braintreepayments/api/interfaces/PayPalTwoFactorAuthCallback.java"
    exclude "com/braintreepayments/api/PreferredPaymentMethods.java"
    exclude "com/braintreepayments/api/models/PreferredPaymentMethodsResult.java"
    exclude "com/braintreepayments/api/interfaces/PreferredPaymentMethodsListener.java"
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

signing {
    required {
        !version.endsWith("SNAPSHOT") && !version.endsWith("DEVELOPMENT")
    }
    sign publishing.publications
}

nexusPublishing {
    // give nexus sonatype more time to initialize the staging repository
    clientTimeout = Duration.ofMinutes(3)
    repositories {
        sonatype()
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocsJar

                groupId = 'com.braintreepayments.api'
                artifactId = 'braintree-core'
                version = rootProject.versionName

                pom {
                    name = 'braintree-core'
                    packaging = 'aar'
                    description = 'This is Braintree\'s Android SDK. Integrate this library to accept payments in your Android app.'
                    url = 'https://github.com/braintree/braintree_android'

                    scm {
                        url = 'scm:git@github.com:braintree/braintree_android.git'
                        connection = 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection = 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

// endregion
