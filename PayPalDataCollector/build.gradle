apply plugin: 'com.android.library'

def dataCollectorVersion = 'paypal-data-collector-3.1.5.jar'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compile 'com.android.support:support-annotations:26.0.0'

    compile files('libs/' + dataCollectorVersion)

    androidTestCompile 'com.android.support.test:runner:1.0.0'
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task releaseJar(type: Jar, dependsOn: android.libraryVariants.release.javaCompile) {
    from projectDir.path + '/build/intermediates/classes/release/'
    exclude { details -> details.file.name.endsWith('.xml') }

    from {
        configurations.compile.findAll {
            it.getName() == dataCollectorVersion
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

artifacts {
    archives javadocsJar
    archives sourcesJar
    archives releaseJar
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.paypal"
}

group = 'com.paypal.android.sdk'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'data-collector'

            pom.project {
                name 'data-collector'
                groupId = 'com.paypal.android.sdk'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'Fraud tools for PayPal integrations on Android.'
            }

            pom.whenConfigured { MavenPom pom ->
                pom.dependencies.each {
                    if (it.artifactId == 'support-annotations') {
                        it.optional = true
                    }
                }
            }
        }
    }
}
