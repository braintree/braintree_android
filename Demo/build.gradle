plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'kotlin-android'
}

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.braintreepayments.demo"
        minSdkVersion 23
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        disable 'IconMissingDensityFolder'
        warning 'InvalidPackage'
        warning 'GradleCompatible'
        textReport true
        textOutput 'stdout'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    buildFeatures {
        dataBinding = true
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("11"))
        }
    }

    namespace 'com.braintreepayments.demo'
}

dependencies {
    implementation 'androidx.preference:preference:1.2.1'
    implementation deps.kotlinStdLib

    implementation('com.squareup.retrofit:retrofit:1.9.0') {
        exclude module: 'com.google.gson'
    }

    implementation project(':AmericanExpress')
    implementation project(':Card')
    implementation project(':GooglePay')
    implementation project(':LocalPayment')
    implementation project(':PayPal')
    implementation project(':DataCollector')
    implementation project(':PayPalMessaging')
    implementation project(':ShopperInsights')
    implementation project(':SEPADirectDebit')
    implementation project(':ThreeDSecure')
    implementation project(':Venmo')
    implementation project(':VisaCheckout')

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'de.greenrobot:eventbus:2.4.1'
    implementation deps.appCompat
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'io.card:android-sdk:5.5.1'
    implementation 'com.braintreepayments:card-form:5.4.0'

    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.navigation:navigation-fragment:2.7.5'
    implementation 'androidx.navigation:navigation-ui:2.7.5'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    def composeBom = platform('androidx.compose:compose-bom:2024.06.00')
    implementation composeBom
    implementation 'androidx.compose.material3:material3'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'

    androidTestImplementation 'com.braintreepayments:device-automator:1.0.0'
    androidTestImplementation project(':TestUtils')

    testImplementation 'junit:junit:4.13.2'
}

task demoApkSize {
  group 'Braintree'
  description 'Prints the current size of the Demo apk'

  def apkDir = fileTree('./build/outputs/apk')
  def apks = apkDir.matching {
    include '*.apk'
    exclude '*unaligned*'
  }
  apks.visit { apk ->
    def apkName = apk.getName()
    def mb = apk.getSize() / 1024 / 1000
    mb = mb.setScale(2, BigDecimal.ROUND_UP)
    println "Size of ${apkName} is ${mb} MB"
  }
}

project.tasks.findAll {
  if (it.name.startsWith('assemble')) {
    it.finalizedBy demoApkSize
  }
}
