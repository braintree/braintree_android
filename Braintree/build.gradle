apply plugin: 'com.android.library'

def DEVELOPMENT_URL = System.properties['DEVELOPMENT_URL'] ?: '"http://10.0.2.2:3000/"';

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        consumerProguardFiles 'proguard.pro'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
            }
        }

        // Visa Checkout
        dataBinding {
            enabled = true
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        androidTest {
            java.srcDir sharedTestDir
        }
        test {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-wallet:[9.0.0,10.0.0)'
    compile 'com.android.support:support-annotations:[25.0.0,26.0.0)'
    compile(name: 'VisaCheckout-Android-SDK', version: '4.6.0', ext: 'aar')

    compile project(':Core')
    compile project(':BraintreeDataCollector')
    compile project(':PayPalOneTouch')

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile project(':TestUtils')

    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.powermock:powermock-module-junit4:1.6.6'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testCompile 'org.powermock:powermock-api-mockito:1.6.6'
    testCompile 'org.powermock:powermock-classloading-xstream:1.6.6'
    testCompile 'org.skyscreamer:jsonassert:1.4.0'
    testCompile project(':TestUtils')

    // Visa Checkout
    androidTestCompile fileTree(dir: "${rootDir}/libs", include: [
            'Samsung-sdk-v1.0.0.jar',
            'Samsung-pass-v1.1.4.jar',
            'eventbus-2.4.0.jar'])
    androidTestCompile 'com.google.android.gms:play-services-basement:9.8.0'
    androidTestCompile 'com.google.code.gson:gson:2.3.1'
    androidTestCompile 'com.squareup.okhttp:okhttp:2.5.0'
    androidTestCompile 'com.squareup.okio:okio:1.6.0'
    androidTestCompile 'com.squareup.retrofit:retrofit:1.9.0'

    // Visa Checkout
    compile 'com.android.support:support-v4:[25.0.0, 26.0.0)'
    compile 'com.android.support:appcompat-v7:[25.0.0, 26.0.0)'
    compile 'com.android.support:design:[25.0.0, 26.0.0)'

    testCompile fileTree(dir: "${rootDir}/libs", include: [
            'Samsung-sdk-v1.0.0.jar',
            'Samsung-pass-v1.1.4.jar',
            'eventbus-2.4.0.jar'])
    testCompile 'com.google.android.gms:play-services-analytics:9.8.0'
    testCompile 'com.google.android.gms:play-services-ads:9.8.0'
    testCompile 'com.google.android.gms:play-services-basement:9.8.0'
    testCompile 'com.parse.bolts:bolts-android:1.4.0'
    testCompile 'com.google.code.gson:gson:2.3.1'
    testCompile 'com.squareup.okhttp:okhttp:2.5.0'
    testCompile 'com.squareup.okio:okio:1.6.0'
}

android.buildTypes.each { type ->
    type.buildConfigField 'String', 'DEVELOPMENT_URL', DEVELOPMENT_URL
}

task runAllTests {
    /*
     * Empty task to run via command line Rakefile or ci.sh to indicate that tests that depend on
     * setting up external services (like the test invalid https server) are set up correctly.
     * Otherwise, these tests will be skipped
     */
}

gradle.taskGraph.whenReady { taskGraph ->
    def RUN_ALL_TESTS = taskGraph.hasTask(':Braintree:runAllTests')
    android.buildTypes.debug.buildConfigField('boolean', 'RUN_ALL_TESTS', '' + RUN_ALL_TESTS)
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.braintreepayments"
}

group = 'com.braintreepayments.api'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'braintree'

            pom.project {
                name 'braintree'
                groupId = 'com.braintreepayments.api'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'This is Braintree\'s v.zero SDK for Android. Integrate this library to accept payments in your Android app.'
            }

            pom.whenConfigured { MavenPom pom ->
                ignoredArtifactIds = [
                        'play-services-wallet',
                        'support-annotations',
                        'data-collector',
                        'VisaCheckout-Android-SDK',
                        'support-v4', // Visa Checkout dependency
                        'appcompat-v7', // Visa Checkout dependency
                        'design' // Visa Checkout dependency
                ]
                pom.dependencies.each { dep ->
                    ignoredArtifactId.each {
                        it.optional = it.artifactId == ignoredArtifactId
                    }
                }
            }
        }
    }
}
