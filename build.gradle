// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'

        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"

        classpath 'org.rauschig:jarchivelib:0.6.0'
        classpath 'commons-io:commons-io:2.4'
        classpath files('libs/0.12.1-SNAPSHOT/gradle-plugin-0.12.1-SNAPSHOT.jar')
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/releases/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    apply plugin: 'android-sdk-manager'
    sdkManager {
        emulatorVersion 'android-22'
    }

    apply plugin: 'io.codearte.nexus-staging'
    nexusStaging {
        packageGroup = "com.braintreepayments"
    }

    apply plugin: 'maven'
    def sonatypeUsername = System.properties.containsKey('sonatypeUsername') ? System.properties['sonatypeUsername'] : ''
    def sonatypePassword = System.properties.containsKey('sonatypePassword') ? System.properties['sonatypePassword'] : ''
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    url 'https://github.com/braintree/braintree_android'

                    scm {
                        url 'scm:git@github.com:braintree/braintree_android.git'
                        connection 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id 'devs'
                            name 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }


    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }

    ext {
        compileSdkVersion = 23
        buildToolsVersion = '23.0.1'
        minSdkVersion = 15
        targetSdkVersion = 23
        versionCode = 33
        versionName = '2.0.0-SNAPSHOT'
    }
}
