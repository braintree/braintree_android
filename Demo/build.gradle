apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.braintreepayments.demo"
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
    }

    lintOptions {
        disable 'IconMissingDensityFolder'
        warning 'InvalidPackage'
        warning 'GradleCompatible'
        textReport true
        textOutput 'stdout'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    dataBinding {
        enabled = true
    }
}

configurations.all {
    // Force remove dependency from play-services-wallet
    exclude group: 'com.google.android.gms', module: 'play-services-maps'
}

def googlePayDependencies = [
        'com.braintreepayments.api:google-payment:3.0.0-SNAPSHOT',
        'com.google.android.gms:play-services-wallet:16.0.1'
]

def visaCheckoutDependencies = [
        'com.braintreepayments.api:visa-checkout:2.0.0',
        'com.visa.checkout:visacheckout-android-sdk:6.6.0@aar',
        'com.squareup.okio:okio:1.13.0',
        'com.squareup.okhttp3:okhttp:3.8.1',
        'com.google.code.gson:gson:2.8.1',
        'de.greenrobot:eventbus:2.4.1',
        'androidx.appcompat:appcompat:1.0.1',
        'androidx.preference:preference:1.0.0',
        'com.google.android.material:material:1.0.0',
        'androidx.cardview:cardview:1.0.0',
        'androidx.multidex:multidex:2.0.0'
]

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'

    implementation('com.squareup.retrofit:retrofit:1.9.0') {
        exclude module: 'com.google.gson'
    }

    implementation project(':Braintree')

    implementation googlePayDependencies
    implementation visaCheckoutDependencies

    implementation 'io.card:android-sdk:5.5.1'
    implementation('com.braintreepayments.api:drop-in:4.0.0-SNAPSHOT') {
        exclude group: 'com.braintreepayments.api', module: 'braintree'
        because 'Use the development version of Braintree'
    }

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4'

    androidTestImplementation 'com.lukekorth:device-automator:1.0.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.1.0-beta02'
    androidTestImplementation project(':TestUtils')

    testImplementation 'junit:junit:4.12'
}



task demoApkSize {
  group 'Braintree'
  description 'Prints the current size of the Demo apk'

  def apkDir = fileTree('./build/outputs/apk')
  def apks = apkDir.matching {
    include '*.apk'
    exclude '*unaligned*'
  }
  apks.visit { apk ->
    def apkName = apk.getName()
    def mb = apk.getSize() / 1024 / 1000
    mb = mb.setScale(2, BigDecimal.ROUND_UP)
    println "Size of ${apkName} is ${mb} MB"
  }
}

project.tasks.findAll {
  if (it.name.startsWith('assemble')) {
    it.finalizedBy demoApkSize
  }
}
